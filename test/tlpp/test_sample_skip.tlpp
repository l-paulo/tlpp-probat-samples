#include "tlpp-core-dev.th"
#include "tlpp-probat.th"

namespace       test.unit.tlpp

using namespace tlpp.probat
using namespace sample.tlpp


/*-------------------------------------------------------------/
DOC Oficial:
https://tdn.totvs.com/display/tec/e-4+-+Pulando+Testes
/-------------------------------------------------------------*/


/*-------------------------------------------------------------/
Skip em metodo
/-------------------------------------------------------------*/
@TestFixture(owner='sample')
class test_class_skip

  private data xValue
  private data xExpected

  public method new()     constructor

  // Tests
  @Test('Exemplo para uso do PROBAT.')
  public method ExampleForUse()

  @Test('Exemplo para uso do PROBAT. SKIP ')
  @Skip()
  public method ExampleForUseSkip()

  @Test('Exemplo para uso do PROBAT. SKIP ASSERT')
  public method ExampleForUseSkipAssert()

endclass

method new() class test_class_skip
  ::xValue      := Nil
  ::xExpected   := Nil
return self

method ExampleForUse() class test_class_skip
  local cTest := 'test_class'
  ::xValue    := sample.tlpp.sampleOK( cTest )
  ::xExpected := cTest
    assertEquals( ::xValue, ::xExpected )
return .T.

method ExampleForUseSkip() class test_class_skip
  assertError( 'nao deve registrar esse assert' )
return .T.

method ExampleForUseSkipAssert() class test_class_skip

  SKIPASSERT
  assertError( 'nao deve registrar esse assert' )

  assertOk( 'registra esse assert por ExampleForUseSkipAssert'  )
return .T.


/*-------------------------------------------------------------/
Exemplo do mesmo teste so que em FUNCTION (com SKIP)
/--------------------------------------------------------------*/
@TestFixture(owner='sample')
@Skip()
function test_function_Skip()
  assertError( 'nao deve registrar esse assert' )
return .T.


/*-------------------------------------------------------------/
Exemplo do mesmo teste so que em FUNCTION (com SKIP ASSERT)
/--------------------------------------------------------------*/
@TestFixture(owner='sample')
function test_function_Skip_Assert()

  SKIPASSERT
  assertError( 'nao deve registrar esse assert' )

  assertOk( 'registra esse assert por test_function_Skip_Assert'  )

return .T.


/*-------------------------------------------------------------/
Exemplos de Skip com Filtro
/--------------------------------------------------------------*/

@TestFixture(owner='sample')
@Skip(tlppVersion=">= 01.02.10")
function test_function_Skip_1()
  assertError( 'nao deve registrar esse assert' )
return .T.

@TestFixture(owner='sample')
@Skip(system="windows")
function test_function_Skip_2()
  assertError( 'nao deve registrar esse assert' )
return .T.

@TestFixture(owner='sample')
@Skip(appServerName="LOBO-GUARA")
function test_function_Skip_3()
  assertError( 'nao deve registrar esse assert' )
return .T.

@TestFixture(owner='sample')
@Skip(system="windows",appServerName="LOBO-GUARA",tlppVersion=">= 01.02.10")
function test_function_Skip_4()
  assertError( 'nao deve registrar esse assert' )
return .T.

@TestFixture(owner='sample')
function test_function_Skip_5()

  SKIPASSERT TLPPVERSION ">= 01.02.10"
  assertError( 'nao deve registrar esse assert' )

  SKIPASSERT SYSTEM "windows"
  assertError( 'nao deve registrar esse assert' )

  SKIPASSERT APPSERVERNAME "LOBO-GUARA"
  assertError( 'nao deve registrar esse assert' )

  SKIPASSERT SYSTEM "windows" APPSERVERNAME "LOBO-GUARA" TLPPVERSION ">= 01.02.10"
  assertError( 'nao deve registrar esse assert' )
return .T.
